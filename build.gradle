/*
 * Copyright 2020 Reshift Security Intellij plugin contributors
 *
 * This file is part of Reshift Security Intellij plugin.
 *
 * Reshift Security Intellij plugin is free software: you can redistribute it
 * and/or modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation, either version 3 of
 * the License, or (at your option) any later version.
 *
 * Reshift Security Intellij plugin is distributed in the hope that it will
 * be useful, but WITHOUT ANY WARRANTY; without even the implied
 * warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Reshift Security Intellij plugin.
 * If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "org.jetbrains.intellij" version "0.6.5"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'maven-publish'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

intellij {
    version ijVersion

    pluginName 'reshiftsecurity-intellij'
    updateSinceUntilBuild false
    type 'IC'

    plugins 'git4idea'
    plugins 'java'
}

runIde {
    systemProperties(["jdk.util.zip.ensureTrailingSlash":false])
    if (System.getenv("ANDROID_STUDIO_PATH")) {
        ideDirectory System.getenv("ANDROID_STUDIO_PATH")
    }
}

/*
FAILURE: Build failed with an exception.

* What went wrong:
A problem occurred evaluating root project 'spotbugs-idea'.
> Could not find method runIdea() for arguments [build_bx44c5s67a78hdgfid54wfjj6$_run_closure4@75342e13] on root project 'spotbugs-idea' of type org.gradle.api.Project.

runIdea {
    systemProperties(["jdk.util.zip.ensureTrailingSlash":false])
}
*/

configurations {
    thirdPartyPlugins
    fbContribJava6
}

dependencies {
    implementation ('com.github.spotbugs:spotbugs:4.0.3') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation 'info.clearthought:table-layout:4.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.8.1.202007141445-r'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.6.2'
    testImplementation 'org.mockito:mockito-core:2.7.6'
    thirdPartyPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.7'
    thirdPartyPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    fbContribJava6 'com.mebigfatguy.fb-contrib:fb-contrib:6.2.1'
}

task copyGradleProperties(type: Copy) {
    description 'Copy gradle.properties to project resources.'
    from 'gradle.properties'
    into 'build/resources/main/com/reshiftsecurity/plugins/intellij/common'
    rename { _ -> 'version.properties' }
}

task downloadThirdPartyPlugins(type: Copy) {
    description 'Downloads third-party plugins Find Security Bugs and FB-Contrib.'
    from configurations.thirdPartyPlugins
    from configurations.fbContribJava6
    into 'build/resources/main/com/reshiftsecurity/plugins/intellij/plugins'
}

task copyThirdPartyPlugins(type: Copy) {
    dependsOn(downloadThirdPartyPlugins, prepareSandbox)
    description 'Copy plugins into sandbox.'
    from 'build/resources/main/com/reshiftsecurity/plugins/intellij/plugins'
    into 'build/idea-sandbox/plugins/reshiftsecurity-intellij/customPlugins'
}

tasks.compileJava.dependsOn(downloadThirdPartyPlugins, copyGradleProperties)
tasks.buildPlugin.dependsOn(copyThirdPartyPlugins)

publishPlugin {
    token = System.getenv("JB_PUBLISH_SECRET")
}

wrapper {
    gradleVersion = '6.3'
}